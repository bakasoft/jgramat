Json    = Object | Array | String | Number | Boolean | Null
Object  = @object(`{` ws { @set-key-value(String, ws `:` ws, Json) ws / `,` ws } `}`)
Array   = @list(`[` ws { @add(Json) ws / `,` ws } `]`)
String  = @value(`"` { escaped-char | <`"`> } `"`)
Number  = @value([`-`] (`0` | `1..9` {digit}) [ `.` {+digit} ] [ `e E` [`- +`] {+digit} ])
Boolean = @value("true" | "false")
Null    = @value("null")

#define esc-map {
  '\\"': '"'       '\\\\': '\\'
  '\\/': '/'       '\\n': '\n'
  '\\r': '\r'      '\\t': '\t'
  '\\b': '\u0008'  '\\f': '\u000C'
}
escaped-char = @replace:%esc-map("\\" `" \\ / b f n r t`)
  | @ignore("\\u") @transform:hex-to-char({4 hex})
hex = `a..f A..F 0..9`
digit = `0..9`
ws = {`\s \t \r \n`}
